PROCEDURE reportScript
STARTREPORT
NOHEADERS
NOGRIDLINES
API_START
... seclist is the various sections that will be used in profit and loss and balance sheet reports
... plan to move this out to ini file or transaction model later
DEFAULT seclist[1]='Bank'
DEFAULT seclist[2]='Receivables'
DEFAULT seclist[3]='Other Current Asset'
DEFAULT seclist[4]='Fixed Assets'
DEFAULT seclist[5]='Payables'
DEFAULT seclist[6]='Other Current Liability'
DEFAULT seclist[7]='Equity'
DEFAULT seclist[8]='Income'
DEFAULT seclist[9]='Cost of Goods Sold'
DEFAULT seclist[10]='Expenses'
DEFAULT seclist[11]='Other Income'
DEFAULT seclist[12]='Other Expense'
... pnlseclist defines which section are used in Profit and Loss, again this can be either an ini file or a transaction model
... the items in this list MUST exist in seclist
DEFAULT pnlseclist[1]='Income'
DEFAULT pnlseclist[2]='Cost of Goods Sold'
DEFAULT pnlseclist[3]='Expenses'
DEFAULT pnlseclist[4]='Other Income'
DEFAULT pnlseclist[5]='Other Expense'
... bsseclist defines which sections used in Balance Sheet, again this can be either an ini file or a transaction model
... the items in this list MUST exist in seclist
DEFAULT bsseclist[1]='Bank'
DEFAULT bsseclist[2]='Receivables'
DEFAULT bsseclist[3]='Other Current Assets'
DEFAULT bsseclist[4]='Fixed Assets'
DEFAULT bsseclist[5]='Payables'
DEFAULT bsseclist[6]='Other Current Liability'
DEFAULT bsseclist[7]='Equity'
... initial list arrays used to store the COA data
DEFAULT accname[1]=' '
DEFAULT accsection[1]=' '
DEFAULT acctype[1]=' '
DEFAULT accparent[1]=' '
PROJECT #_apiLibrary
MODEL 'CommonCOA'
... load all the data from the trasnaction model into list variables
READ MEMORY 'All'
accname=SERIES3
accsection=SERIES4
acctype=SERIES5
accparent=SERIES6
nritems=ITEMSINFO('accname','ITEMS')
...
... create Profit and Loss Measures List
...
mcount=0
quot='''
...loop through each account belonging to the profit and loss section list to build the logic entries for each measure
... Each section will have a header entry and at the end a total measure
... All DETAIL items will consolidated into a SECTION or into a SUMMARY level
FOR j,1,ITEMSINFO('pnlseclist','ITEMS'),1
    ... create the section heading entry and initialise the section total logic
    indent=0
    mcount=mcount+1
    measurelist[mcount]=pnlseclist[j]
    mexpression[mcount]=' '
    mindent[mcount]=indent
    mhide[mcount]='hideData'
    mbold[mcount]=1
    secexpression=' '
    ... loop through finding all accounts belonging to this section
    FOR k,1,nritems,1
        WHEN accsection[k] EQ pnlseclist[j]
            indent=4
            mcount=mcount+1
            measurelist[mcount]=accname[k]
            mhide[mcount]='showData'
            mbold[mcount]=0
            mexpression[mcount]=' '
            ... if this is directly below SECTION then add it to the SECTION expression
            ... otherwise it is DETAIL subaccount, so indent further
            WHEN accparent[k] EQ pnlseclist[j]
                secexpression=concat(secexpression,'+',quot,accname[k],quot)
            ELSE
                indent=8
            ENDWHEN
            mindent[mcount]=indent
            ... if this is a SUMMARY account, find all the accounts belonging to it and add them to the expression
            ... for this measure
            SWITCH
            	CASE lowercase(acctype[k]) EQ 'summary'
            		mbold[mcount]=1
            		subseca=accname[k]
            		FOR m,1,nritems,1
            			WHEN accparent[m] EQ subseca
                        		mexpression[mcount]=concat(mexpression[mcount],'+',quot,accname[m],quot)
                        	ENDWHEN
                        ENDFOR
                CASE lowercase(acctype[k]) EQ 'detail'
                	mbold[mcount]=0
                	mexpression[mcount]=concat('Trial_Balance.',quot,measurelist[mcount],quot,'*ElimFlags.',quot,measurelist[mcount],quot,'[1]')
            ENDSWITCH
        ENDWHEN
    ENDFOR
... end of SECTION so create the section total measure
    mcount=mcount+1
    measurelist[mcount]=concat('Total ',pnlseclist[j])
    mindent[mcount]=0
    mhide[mcount]='showData'
    mbold[mcount]=1
    mexpression[mcount]=secexpression
... create pnl calcs at correct place
    SWITCH
    CASE measurelist[mcount] eq 'Total Cost of Goods Sold'
        mcount=mcount+1
        measurelist[mcount]='Gross Profit'
        mindent[mcount]=0
        mhide[mcount]='showData'
        mbold[mcount]=1
        mexpression[mcount]=concat(quot,'Total Income',quot,'-',quot,'Total Cost of Goods Sold',quot)
    CASE measurelist[mcount] eq 'Total Expenses'
        mcount=mcount+1
        measurelist[mcount]='Net Operating Profit'
        mindent[mcount]=0
        mhide[mcount]='showData'
        mbold[mcount]=1
        mexpression[mcount]=concat(quot,'Gross Profit',quot,'-',quot,'Total Expenses',quot)
    CASE measurelist[mcount] eq 'Total Other Expense'
        mcount=mcount+1
        measurelist[mcount]='Net Profit'
        mindent[mcount]=0
        mhide[mcount]='showData'
        mbold[mcount]=1
        mexpression[mcount]=concat(quot,'Net Operating Profit',quot,'+',quot,'Total Other Income',quot,'-',quot,'Total Other Expense',quot)
    ENDSWITCH
ENDFOR
... Now loop through and show what we have
FOR j,1,mcount,1
    columnwidth 300,1-2
    TEXT measurelist[j]
    SAMELINE
    TEXT mexpression[j]
    TEXT mhide[j]
    TEXT mindent[j]
    DIFFERENTLINE
ENDFOR
... Update the Profit and Loss model with the new measures
PROJECT #_apiLibrary
MODEL 'Profit and Loss'
api_measure
     with dimension = 'measure'
     with option = 'ADD'
     with sourceMember = measurelist
     with expression = mexpression
     with measureIndent = mindent
     with hide = mhide
     with measureBold = mbold
... following code was to try and do an update (i.e. add new and modify old, but doesn't appear to work, so
... disabled for now
.oldlist=_modelmeasures
.columnwidth 250,1-10
.FOR j,1,mcount,1
.    smname=measurelist[j]
.    smexpression=mexpression[j]
.    smindent=mindent[j]
.    smhide=mhide[j]
.    smbold=mbold[j]
.    smafter[1]=' '
.    WHEN inlist(smname,'oldlist',1,'MATCH') GT 0
.        option='modify'
.    else
.        option='add'
.        WHEN j GT 1
.            smafter[1]=measurelist[j-1]
.        ELSE
.            smafter[1]='HiddenDummyMeasure'
.        ENDWHEN
.    endwhen
.    TEXT 'PROCESSING'
.    SAMELINE
.    TEXT smname
.    TEXT option
.   TEXT smindent
.    TEXT smhide
.    TEXT smexpression
.    TEXT smafter
.    TEXT smbold
.    DIFFERENTLINE
.    SWITCH
.        CASE option EQ 'add'
.            api_measure
.                with dimension = 'measure'
.                with option = #option
.                with sourceMember = smname
.                with expression = smexpression
.                with measureIndent = smindent
.                with hide = smhide
.                with measureBold = smbold
.                with aftermember = smafter
.        CASE option EQ 'modify'
.            api_measure
.                with dimension = 'measure'
.                with option = #option
.                with sourceMember = smname
.                with expression = smexpression
.                with measureIndent = smindent
.                with hide = smhide
.    ENDSWITCH
.ENDFOR
ENDREPORT
API_END
RETURN
