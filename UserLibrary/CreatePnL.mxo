PROCEDURE reportScript
.///////////////////////////////////////////////////////////////////////////////
.// Procedure: CreatePnL
.// Author: Greg Parsons
.// This procedure creates the measure logic for the Profit and Loss model using
.// the detail in the CommonCOA table. 
.// 
.///////////////////////////////////////////////////////////////////////////////
DEFAULT taskstatus=0
DEFAULT taskstatdesc=' '
API_START
... initial list arrays used to store the COA data
DEFAULT accname[1]=' '
DEFAULT accsection[1]=' '
DEFAULT acctype[1]=' '
DEFAULT accparent[1]=' '
.// These are the default settings for idump and reset
.// to change these edit the deafults.ini file in the projects USERDATA folder
.// set idump = 1 if you require additional information dumped when the script 
.//               is run
.// set ireset = 1 if you want the script to delete all existing measures and
.//               rebuild from scratch (useful if you need to delete and start
.//               again
DEFAULT idump=0
DEFAULT ireset=1
.// retrieve the seclist, bsslist and pnllist arrays
_STORAGE='sections'
READ STORAGE
_STORAGE='defaults'
READ STORAGE
COPYHTML <B>Building Profit and Loss model for project 
COPYHTML #_apilibrary
COPYHTML </B></BR>
PROJECT #_apiLibrary
MODEL 'CommonCOA'
... load all the data from the trasnaction model into list variables
READ MEMORY 'All'
WHEN ITEMSINFO('SERIES1','ITEMS') NE 0
	accname=SERIES3
	accsection=SERIES4
	acctype=SERIES5
	accparent=SERIES6
	nritems=ITEMSINFO('accname','ITEMS')
	CLOSE DATABASE
	...
	... create Profit and Loss Measures List
	...
		mcount=0
		quot='''
	...loop through each account belonging to the profit and loss section list to build the logic entries for each measure
	... Each section will have a header entry and at the end a total measure
	... All DETAIL items will consolidated into a SECTION or into a SUMMARY level
		FOR j,1,ITEMSINFO('pnlseclist','ITEMS'),1
	... create the section heading entry and initialise the section total logic
		    indent=0
		    mcount=mcount+1
		    measurelist[mcount]=pnlseclist[j]
		    mexpression[mcount]=' '
		    mindent[mcount]=indent
		    mhide[mcount]='hideData'
		    mbold[mcount]=1
		    mperiodoption[mcount]='SUM'
		    secexpression=' '
	... loop through finding all accounts belonging to this section
		    FOR k,1,nritems,1
			WHEN accsection[k] EQ pnlseclist[j]
				indent=4
				mcount=mcount+1
				measurelist[mcount]=accname[k]
				mhide[mcount]='showData'
				mbold[mcount]=0
				mexpression[mcount]=' '
				mperiodoption[mcount]='SUM'
		    ... if this is directly below SECTION then add it to the SECTION expression
		    ... otherwise it is DETAIL subaccount, so indent further
				WHEN accparent[k] EQ pnlseclist[j]
					secexpression=concat(secexpression,'+',quot,accname[k],quot)
				ELSE
					indent=8
				ENDWHEN
				mindent[mcount]=indent
		    ... if this is a SUMMARY account, find all the accounts belonging to it and add them to the expression
		    ... for this measure
				SWITCH
					CASE lowercase(acctype[k]) EQ 'summary'
						mbold[mcount]=1
						subseca=accname[k]
						FOR m,1,nritems,1
							WHEN accparent[m] EQ subseca
								mexpression[mcount]=concat(mexpression[mcount],'+',quot,accname[m],quot)
							ENDWHEN
						ENDFOR
					CASE lowercase(acctype[k]) EQ 'detail'
						mbold[mcount]=0
						mexpression[mcount]=concat('Trial_Balance.',quot,measurelist[mcount],quot,'*ElimFlags.',quot,measurelist[mcount],quot,'[1]')
				ENDSWITCH
			ENDWHEN
		    ENDFOR
	... end of SECTION so create the section total measure
		    mcount=mcount+1
		    measurelist[mcount]=concat('Total ',pnlseclist[j])
		    mindent[mcount]=0
		    mhide[mcount]='showData'
		    mbold[mcount]=1
		    mexpression[mcount]=secexpression
		    mperiodoption[mcount]='SUM'
	... create pnl calcs at correct place
		    SWITCH
			CASE measurelist[mcount] eq 'Total Cost of Goods Sold'
				mcount=mcount+1
				measurelist[mcount]='Gross Profit'
				mindent[mcount]=0
				mhide[mcount]='showData'
				mbold[mcount]=1
				mexpression[mcount]=concat(quot,'Total Income',quot,'-',quot,'Total Cost of Goods Sold',quot)
				mperiodoption[mcount]='SUM'
			CASE measurelist[mcount] eq 'Total Expenses'
				mcount=mcount+1
				measurelist[mcount]='Net Operating Profit'
				mindent[mcount]=0
				mhide[mcount]='showData'
				mbold[mcount]=1
				mexpression[mcount]=concat(quot,'Gross Profit',quot,'-',quot,'Total Expenses',quot)
				mperiodoption[mcount]='SUM'
			CASE measurelist[mcount] eq 'Total Other Expense'
				mcount=mcount+1
				measurelist[mcount]='Net Profit'
				mindent[mcount]=0
				mhide[mcount]='showData'
				mbold[mcount]=1
				mexpression[mcount]=concat(quot,'Net Operating Profit',quot,'+',quot,'Total Other Income',quot,'-',quot,'Total Other Expense',quot)
				mperiodoption[mcount]='SUM'
		    ENDSWITCH
		ENDFOR
		UpdateModelMeasures
			with modelname='Profit and Loss'
			with dimname='Measure'
			with measurelist=measurelist
			with mexpression=mexpression
			with mindent=mindent
			with mhide=mhide
			with mbold=mbold
			with mperiodoption=mperiodoption
			with ireset=ireset
			with idump=idump
			with populate=1
		taskstatus=1
		FORMAT DATE 'DD-MM-YYYY' FOR _today
		FORMAT '99:99:99' FOR _time
		DECODE _TODAY INTO taskdate TYPE TEXT AT 1 TO 10
		DECODE _TIME INTO tasktime TYPE TEXT AT 1 TO 8
		taskstatdesc=concat('Last run on ',taskdate,' at ',tasktime)
ELSE
	COPYHTML You must load the Common Chart of Accounts before you can create the Profit and Loss
	taskstatus=0
	taskstatdesc='Not run'
ENDWHEN
API_END
RETURN
